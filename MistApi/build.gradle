apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties"))) //Set env variable GPR_USER & GPR_API_KEY if not adding a properties file


def getGitVersion = { ->
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine 'git', 'describe', '--abbrev=4', '--always', '--tags'
        standardOutput = stdout
    }


    return stdout.toString().trim()
}

def getGitClean = { ->

    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'status', '--porcelain'
        standardOutput = stdout

    }
    if ("${stdout.size()}" > 0) {
        return "-d"
    } else {
        return "-c"
    }
}

def libraryGroupId = 'fi.ct.mist'
def libraryArtifactId = 'mistapi' //archivesBaseName
def libraryVersion = getGitVersion()

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    def git_version = getGitVersion

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28

        buildConfigField "String", "GitVersion", "\"${git_version}\""
        buildConfigField "String", "GitClean", "\"${getGitClean()}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            libraryVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${libraryArtifactId}-${libraryVersion}.aar"
                }
            }
        }
    }

    lintOptions {
        disable 'InvalidPackage','InvalidPackage'
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'org.mongodb:bson:3.6.0'
    implementation 'org.apache.commons:commons-compress:1.12'
    implementation 'com.squareup.okio:okio:1.15.0'
    implementation 'fi.ct.wish:wishcore:0.9.3'
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId
            artifact("$buildDir/outputs/aar/${artifactId}-${version}.aar")

            //generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }
}

allprojects { currProject ->
    task printDependencies {
        dependsOn 'dependencies'
    }
}




publishing {
  repositories {
          maven {
                name = "GitHubPackages"
          /** Configure path of your package repository on Github
          ** Replace GITHUB_USERID with your/organisation Github userID       
          ** and REPOSITORY with the repository name on GitHub
          */
  url = uri("https://maven.pkg.github.com/ControlThings/mist-api-android")
  credentials {
          /** Create github.properties in root project folder file with     
          ** gpr.usr=GITHUB_USER_ID & gpr.key=PERSONAL_ACCESS_TOKEN 
          ** Set env variable GPR_USER & GPR_API_KEY if not adding a properties file**/
  
          username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
          password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
        }
      }
    }
}

/*
artifactory {
    contextUrl = 'http://foremost.cto.fi:8081/artifactory'
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}
*/
